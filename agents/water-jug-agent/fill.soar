## PROPOSE

sp {water-jug*propose*fill
    (state <s> ^name water-jug-agent
               ^jug <j>)
    (<j> ^empty > 0)
    -->
    (<s> ^operator <o> + =)
    (<o> ^name fill
         ^fill-jug <j>)
}

## SELECT

sp {water-jug*select*operator*avoid*inverse*fill
   (state <s> ^name water-jug-agent
              ^operator <o> +
              ^last-operator <lo>)
   (<o>  ^name fill  ^fill-jug <i>)
   (<lo> ^name empty ^empty-jug <i>)
-->
   (<s> ^operator <o> <)
}

## APPLY

sp {water-jug*apply*fill
   (state <s> ^name water-jug-agent
              ^operator <o>
              ^jug <j>)
   (<o> ^name fill
        ^fill-jug <j>)
   (<j> ^volume <volume>
        ^contents <contents>)
    -->
   (<j> ^contents <volume>)
   (<j> ^contents <contents> -)
}

sp {water-jug*apply*fill*record*last-operator
   (state <s> ^name water-jug-agent
              ^operator <o>)
   (<o> ^name fill
        ^fill-jug <fj>)
-->
   (<s> ^last-operator <last-op>)
   (<last-op> ^name fill
              ^fill-jug <fj>)
}

sp {water-jug*apply*fill*remove*last-operator
   (state <s> ^name water-jug-agent
              ^operator <o>
              ^last-operator <last-op>)
   (<o> ^name fill
        ^fill-jug <fj>)
   -(<last-op> ^name fill
               ^fill-jug <fj>)
-->
   (<s> ^last-operator <last-op> -)
}
